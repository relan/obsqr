buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.0.+'
	}
}

apply plugin: 'com.android.application'

repositories {
	mavenCentral()
}

dependencies {
	compile 'com.jakewharton:butterknife:6.0.+'
}

android {
	compileSdkVersion 'android-22'
	buildToolsVersion '22.0.1'

	defaultConfig {
		minSdkVersion 9
		targetSdkVersion 22
		versionCode 13
		versionName "2.7"
	}

	signingConfigs {
		release
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
			proguardFile 'proguard.txt'
			signingConfig signingConfigs.release
		}
	}

	defaultConfig {
		ndk {
			moduleName "zbar"
			ldLibs "log"
			cFlags "-std=c99"
		}
	}

	lintOptions {
		abortOnError false
	}
}

if (project.hasProperty('storeFile') &&
		project.hasProperty('storePassword') &&
		project.hasProperty('keyAlias') &&
		project.hasProperty('keyPassword')) {
	android.signingConfigs.release.storeFile = file(storeFile)
	android.signingConfigs.release.storePassword = storePassword
	android.signingConfigs.release.keyAlias = keyAlias
	android.signingConfigs.release.keyPassword = keyPassword
} else {
	android.buildTypes.release.signingConfig = null
}

task appStart(type: Exec, dependsOn: ':installDebug') {
	commandLine 'adb', 'shell', 'am', 'start', '-n', 'trikita.obsqr/.ObsqrActivity'
}

